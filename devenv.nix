{ inputs
, pkgs
, lib
, config
, options
, ...
}:
{
  env = {
    # The path to the eval cache database (for migrations)
    DATABASE_URL = "sqlite:.devenv/nix-eval-cache.db";

    # The Nix CLI for devenv to use when run with `cargo run`.
    DEVENV_NIX = inputs.nix.packages.${pkgs.stdenv.system}.nix-cli;

    RUST_LOG = "devenv=debug";
    RUST_LOG_SPAN_EVENTS = "full";
  };

  # Configure Claude Code
  claude.code = {
    enable = true;
    commands = {
      release = ''
        Release devenv version $ARGUMENTS

        1. Update version in Cargo.toml to $ARGUMENTS using `cargo set-version`
        2. Run `cargo check` to update Cargo.lock
        3. Generate release notes:
           - Find the latest git tag
           - Get the diff between that tag and HEAD
           - Summarize changes into release notes format
        4. Ask user for access to nixpkgs repository to bump devenv there
           - The package is at pkgs/by-name/de/devenv/package.nix
           - Sync with ./package.nix from this repo and bump version
        5. If this is a major version bump (X.Y.0, not X.Y.Z patch):
           - Generate a blog post in docs/src/blog/posts/
           - Follow the naming convention: devenv-vX.Y-short-description.md
           - Use existing blog posts as reference for format and style
      '';
    };
    permissions = {
      WebFetch = {
        allow = [
          "domain:github.com"
          "domain:docs.rs"
          "domain:docs.anthropic.com"
        ];
      };
      Bash = {
        allow = [
          "rg:*"
          "cargo test:*"
          "nix search:*"
          "devenv-run-tests:*"
          "nix-instantiate:*"
        ];
      };
    };
  };

  # Project dependencies
  packages = [
    pkgs.git
    pkgs.xorg.libxcb
    pkgs.yaml2json
    pkgs.tesh
    pkgs.watchexec
    pkgs.openssl
    pkgs.sqlx-cli
    pkgs.process-compose
    pkgs.cargo-outdated # Find outdated crates
    pkgs.cargo-machete # Find unused crates
    pkgs.cargo-edit # Adds the set-version command
    pkgs.protobuf # snix
    pkgs.dbus # secretspec
  ];

  languages = {
    # For developing the Nix modules
    nix.enable = true;

    # For developing the devenv CLI
    rust.enable = true;
  };

  devcontainer = {
    enable = true;
    settings.customizations.vscode.extensions = [ "jnoortheen.nix-ide" ];
  };

  difftastic.enable = true;

  scripts.devenv-generate-languages-example = {
    description = "Generate an example enabling every supported language";
    exec = ''
      cat > examples/supported-languages/devenv.nix <<EOF
      # DO NOT MODIFY.
      # This file was generated by devenv-generate-languages-example.
      { pkgs, ... }: {

        # Enable all languages tooling!
        ${lib.concatStringsSep "\n  " (
          map (lang: "languages.${lang}.enable = true;") (builtins.attrNames options.languages)
        )}

        # If you're missing a language, please contribute it by following examples of other languages <3
      }
      EOF
    '';
  };

  scripts.devenv-test-cli = {
    description = "Test devenv CLI.";
    exec = ''
      set -xe
      set -o pipefail

      tmp="$(mktemp -d)"
      devenv init "$tmp"
      pushd "$tmp"
        devenv version
        devenv --override-input devenv path:${config.devenv.root}?dir=src/modules test
      popd
      rm -rf "$tmp"

      # Test devenv init with target path
      tmp="$(mktemp -d)"
      pushd "$tmp"
        devenv init target
        test -z "$(ls -A1 | grep -v target)"
        pushd target
          devenv --override-input devenv path:${config.devenv.root}?dir=src/modules test
        popd
      popd
      rm -rf "$tmp"

      # Test devenv integrated into bare Nix flake
      tmp="$(mktemp -d)"
      pushd "$tmp"
        nix flake init --template ''${DEVENV_ROOT}
        nix flake update \
          --override-input devenv ''${DEVENV_ROOT}
        nix develop --accept-flake-config --no-pure-eval --command echo nix-develop started succesfully |& tee ./console
        grep -F 'nix-develop started succesfully' <./console
        grep -F "$(${lib.getExe pkgs.hello})" <./console

        # Assert that nix-develop fails in pure mode.
        if nix develop --command echo nix-develop started in pure mode |& tee ./console
        then
          echo "nix-develop was able to start in pure mode. This is explicitly not supported at the moment."
          exit 1
        fi
        grep -F 'devenv was not able to determine the current directory.' <./console
      popd
      rm -rf "$tmp"

      # Test devenv integrated into flake-parts Nix flake
      tmp="$(mktemp -d)"
      pushd "$tmp"
        nix flake init --template ''${DEVENV_ROOT}#flake-parts
        nix flake update \
          --override-input devenv ''${DEVENV_ROOT}
        nix develop --accept-flake-config --override-input devenv-root "file+file://"<(printf %s "$PWD") --command echo nix-develop started succesfully |& tee ./console
        grep -F 'nix-develop started succesfully' <./console
        grep -F "$(${lib.getExe pkgs.hello})" <./console
        # Test that a container can be built
        if [ "$(uname)" = "Linux" ]
        then
          nix build --override-input devenv-root "file+file://"<(printf %s "$PWD") --accept-flake-config --show-trace .#container-processes
        fi
      popd
      rm -rf "$tmp"
    '';
  };

  git-hooks.package = pkgs.prek;
  git-hooks.hooks = {
    nixpkgs-fmt.enable = true;
    rustfmt.enable = true;
    markdownlint = {
      settings.configuration = {
        MD013 = {
          line_length = 120;
        };
        MD033 = false;
        MD034 = false;
      };
    };
  };
}
