{ pkgs, lib, config, ... }:

let
  cfg = config.services.minio;
  types = lib.types;
  json = pkgs.formats.json { };

  startScript = ''
    if [[ ! -d "$MINIO_DATA_DIR" ]]; then
      mkdir -p "$MINIO_DATA_DIR"
    fi

    if [[ ! -d "$MINIO_CONFIG_DIR" ]]; then
      mkdir -p "$MINIO_CONFIG_DIR"
    fi

    for bucket in ${lib.escapeShellArgs cfg.buckets}; do
      mkdir -p "$MINIO_DATA_DIR/$bucket"
    done

    exec ${cfg.package}/bin/minio server --json --address ${cfg.listenAddress} --console-address ${cfg.consoleAddress} "--config-dir=$MINIO_CONFIG_DIR" "$MINIO_DATA_DIR"
  '';

  clientWrapper = pkgs.writeShellScriptBin "mc" ''
    mkdir -p "$MINIO_CLIENT_CONFIG_DIR"
    install -m 0644 \
      '${json.generate "mc-config.json" cfg.clientConfig}' \
      "$MINIO_CLIENT_CONFIG_DIR/config.json"
    exec ${cfg.clientPackage}/bin/mc --config-dir "$MINIO_CLIENT_CONFIG_DIR" "$@"
  '';
in
{

  options.services.minio = {
    enable = lib.mkEnableOption "MinIO Object Storage";

    listenAddress = lib.mkOption {
      default = "127.0.0.1:9000";
      type = types.str;
      description = "IP address and port of the server.";
    };

    consoleAddress = lib.mkOption {
      default = "127.0.0.1:9001";
      type = types.str;
      description = "IP address and port of the web UI (console).";
    };

    accessKey = lib.mkOption {
      default = "";
      type = types.str;
      description = ''
        Access key of 5 to 20 characters in length that clients use to access the server.
        This overrides the access key that is generated by MinIO on first startup and stored inside the
        `configDir` directory.
      '';
    };

    secretKey = lib.mkOption {
      default = "";
      type = types.str;
      description = ''
        Specify the Secret key of 8 to 40 characters in length that clients use to access the server.
        This overrides the secret key that is generated by MinIO on first startup and stored inside the
        `configDir` directory.
      '';
    };

    region = lib.mkOption {
      default = "us-east-1";
      type = types.str;
      description = ''
        The physical location of the server. By default it is set to us-east-1, which is same as AWS S3's and MinIO's default region.
      '';
    };

    browser = lib.mkOption {
      default = true;
      type = types.bool;
      description = "Enable or disable access to web UI.";
    };

    package = lib.mkOption {
      default = pkgs.minio;
      defaultText = lib.literalExpression "pkgs.minio";
      type = types.package;
      description = "MinIO package to use.";
    };

    buckets = lib.mkOption {
      default = [ ];
      type = types.listOf types.str;
      description = ''
        List of buckets to ensure exist on startup.
      '';
    };

    clientPackage = lib.mkOption {
      default = pkgs.minio-client;
      defaultText = lib.literalExpression "pkgs.minio-client";
      type = types.package;
      description = "MinIO client package to use.";
    };

    clientConfig = lib.mkOption {
      type = types.nullOr json.type;
      description = ''
        Contents of the mc `config.json`, as a nix attribute set.

        By default, `local` is configured to connect to the devenv minio service.
        Use `lib.mkForce null` to use your regular mc configuration from `$HOME/.mc` instead.
      '';
    };
  };

  config = lib.mkIf cfg.enable {
    processes.minio.exec = "${startScript}";

    env.MINIO_DATA_DIR = config.env.DEVENV_STATE + "/minio/data";
    env.MINIO_CONFIG_DIR = config.env.DEVENV_STATE + "/minio/config";
    env.MINIO_REGION = "${cfg.region}";
    env.MINIO_BROWSER = "${if cfg.browser then "on" else "off"}";
    env.MINIO_ROOT_USER = "${cfg.accessKey}";
    env.MINIO_ROOT_PASSWORD = "${cfg.secretKey}";
    env.MINIO_CLIENT_CONFIG_DIR = config.env.DEVENV_STATE + "/minio/mc";

    packages = [
      (if cfg.clientConfig == null then cfg.clientPackage else clientWrapper)
    ];

    services.minio.clientConfig = lib.mkBefore {
      version = "10";
      aliases.local = {
        url = "http://${if lib.hasPrefix ":" cfg.listenAddress then "localhost:${cfg.listenAddress}" else cfg.listenAddress}";
        inherit (cfg) accessKey secretKey;
        api = "S3v4";
        path = "auto";
      };
    };

  };
}
