{ pkgs, lib, config, ... }:

let
  cfg = config.services.minio;
  types = lib.types;
  startScript = pkgs.writeShellScript "start-minio" ''
    if [[ ! -d "$MINIO_DATA_DIR" ]]; then
      mkdir -p "$MINIO_DATA_DIR"
    fi

    if [[ ! -d "$MINIO_CONFIG_DIR" ]]; then
      mkdir -p "$MINIO_CONFIG_DIR"
    fi

    for bucket in ${lib.escapeShellArgs cfg.buckets}; do
      mkdir -p "$MINIO_DATA_DIR/$bucket"
    done

    exec ${cfg.package}/bin/minio server --json --address ${cfg.listenAddress} --console-address ${cfg.consoleAddress} "--config-dir=$MINIO_CONFIG_DIR" "$MINIO_DATA_DIR"
  '';
in
{

  options.services.minio = {
    enable = lib.mkEnableOption (lib.mdDoc "Minio Object Storage");

    listenAddress = lib.mkOption {
      default = "127.0.0.1:9000";
      type = types.str;
      description = lib.mdDoc "IP address and port of the server.";
    };

    consoleAddress = lib.mkOption {
      default = "127.0.0.1:9001";
      type = types.str;
      description = lib.mdDoc "IP address and port of the web UI (console).";
    };

    accessKey = lib.mkOption {
      default = "";
      type = types.str;
      description = lib.mdDoc ''
        Access key of 5 to 20 characters in length that clients use to access the server.
        This overrides the access key that is generated by minio on first startup and stored inside the
        `configDir` directory.
      '';
    };

    secretKey = lib.mkOption {
      default = "";
      type = types.str;
      description = lib.mdDoc ''
        Specify the Secret key of 8 to 40 characters in length that clients use to access the server.
        This overrides the secret key that is generated by minio on first startup and stored inside the
        `configDir` directory.
      '';
    };

    region = lib.mkOption {
      default = "us-east-1";
      type = types.str;
      description = lib.mdDoc ''
        The physical location of the server. By default it is set to us-east-1, which is same as AWS S3's and Minio's default region.
      '';
    };

    browser = lib.mkOption {
      default = true;
      type = types.bool;
      description = lib.mdDoc "Enable or disable access to web UI.";
    };

    package = lib.mkOption {
      default = pkgs.minio;
      defaultText = lib.literalExpression "pkgs.minio";
      type = types.package;
      description = lib.mdDoc "Minio package to use.";
    };

    buckets = lib.mkOption {
      default = [ ];
      type = types.listOf types.str;
      description = lib.mdDoc ''
        List of buckets to ensure exist on startup.
      '';
    };
  };

  config = lib.mkIf cfg.enable {
    processes.minio.exec = "${startScript}";

    env.MINIO_DATA_DIR = config.env.DEVENV_STATE + "/minio/data";
    env.MINIO_CONFIG_DIR = config.env.DEVENV_STATE + "/minio/config";
    env.MINIO_REGION = "${cfg.region}";
    env.MINIO_BROWSER = "${if cfg.browser then "on" else "off"}";
    env.MINIO_ROOT_USER = "${cfg.accessKey}";
    env.MINIO_ROOT_PASSWORD = "${cfg.secretKey}";
  };
}
